### References
- [A Beginner's Guide to the ESP8266](https://tttapa.github.io/ESP8266/Chap01%20-%20ESP8266.html)
- [How Does the Internet Work?](https://web.stanford.edu/class/msande91si/www-spr04/readings/week1/InternetWhitepaper.htm)
- Arduino IDE example sketches


### Introduction
The ESP8266 is a System on a Chip (SoC), manufactured by the Chinese company Espressif. It consists of a Tensilica L106 32-bit micro controller unit (MCU) and a Wi-Fi transceiver. It has 11 GPIO pins* (General Purpose Input/Output pins), and an analog input as well. This means that you can program it like any normal Arduino or other microcontroller. And on top of that, you get Wi-Fi communication, so you can use it to connect to your Wi-Fi network, connect to the Internet, host a web server with real web pages, let your smartphone connect to it, etc ... The possibilities are endless! It's no wonder that this chip has become the most popular IOT device available.

There are many different modules available, standalone modules like the ESP-## series by AI Thinker, or complete development boards like the NodeMCU DevKit or the WeMos D1. Different boards may have different pins broken out, have different Wi-Fi antennas, or a different amount of flash memory on board.
Typically, you will be using a 3-level system - the SoC (1) is laid on an ESP-## board with flash memory (2) which is further laid out on to a breadboardable MCU (3).

(\*) The ESP8266 chip itself has 17 GPIO pins, but 6 of these pins (6-11) are used for communication with the on-board flash memory chip.


### WeMos D1 Mini specifications
Feature | WeMos D1 Mini | Arduino Uno
------------------|-----------------------|----
Operating Voltage |   3.3V                | 5V
Digital I/O Pins  |   11                  | 14
Analog Input Pins |   1 (Max input: 3.2V) | 6/8
Clock Speed       | 80 MHz/160 MHz        | 16 MHz
Flash             | 4 MB                  | 32 KB
SRAM              | 80 KB                 | 2 KB

### Configuring the Arduino IDE for an ESP8266 board
Follow the instructions [here](https://www.instructables.com/id/Quick-Start-to-Nodemcu-ESP8266-on-Arduino-IDE/).
You may also need to install the [CH340 driver](https://sparks.gogo.co.nz/ch340.html).

### How the internet works
Because the Internet is a global network of computers each computer connected to the Internet must have a unique address. Internet addresses are in the form nnn.nnn.nnn.nnn where nnn must be a number from 0 - 255. This address is known as an IP address. (IP stands for Internet Protocol; more on this later.) If you connect to the Internet through an Internet Service Provider (ISP), you are usually assigned a temporary IP address for the duration of your dial-in session.

#### Protocol stacks and packets
So your computer is connected to the Internet and has a unique address. How does it 'talk' to other computers connected to the Internet? An example should serve here: Let's say your IP address is 1.2.3.4 and you want to send a message to the computer 5.6.7.8. The message you want to send is "Hello computer 5.6.7.8!". Obviously, the message must be transmitted over whatever kind of wire connects your computer to the Internet. Let's say you've dialed into your ISP from home and the message must be transmitted over the phone line. Therefore the message must be translated from alphabetic text into electronic signals, transmitted over the Internet, then translated back into alphabetic text. How is this accomplished? Through the use of a protocol stack. Every computer needs one to communicate on the Internet and it is usually built into the computer's operating system (i.e. Windows, Unix, etc.). The protocol stack used on the Internet is refered to as the TCP/IP protocol stack because of the two major communication protocols used. The TCP/IP stack looks like this:

Protocol Layer | Comments
---------------|----------
Application Protocols Layer | Protocols specific to applications such as HTTP, e-mail, FTP, etc.
Transmission Control Protocol Layer |  TCP directs packets to a specific application on a computer using a port number.
Internet Protocol Layer |    IP directs packets to a specific computer using an IP address.
Hardware Layer | Converts binary packet data to network signals and back. (E.g. ethernet network card, modem for phone lines, etc.)

#### HTTP
HTTP is a connectionless text based protocol. Clients (web browsers) send requests to web servers for web elements such as web pages and images. After the request is serviced by a server, the connection between client and server across the Internet is disconnected. A new connection must be made for each request. Most protocols are connection oriented. This means that the two computers communicating with each other keep the connection open over the Internet. HTTP does not however. Before an HTTP request can be made by a client, a new connection must be made to the server.

#### WebSocket
HTTP is great for things like downloading webpages, uploading photos etc. but it's quite slow: every time you send an HTTP request, you have to start a new TCP connection to the server, then send your request, wait for the server to respond, and download the response. Wouldn't it be great if we didn't have to open a new connection every time we want to send some data, and if we could send and receive data at the same time at any moment we'd like? That's where WebSocket comes to the rescue: you can keep the TCP connection with the server open at all times, you get perfect TCP reliability, and it's pretty fast.


### Connecting to the ESP8266
**Method 1**
```
-----------------           ----------------------            -------------------
|      ESP      |           |    Web server      |            |      Phone      |
| (connected to |  <----->  | tembusutetris.com  |   <----->  |  (connected to  |
|    NUS_STU)   |           |                    |            |     NUS_STU)    |
-----------------           ----------------------            -------------------
```
- Easy to update server code from anywhere
    - Possibly update ESP via server
- If NUS_STU is down, game is down
    - Alternatively implement method 2 as fallback
        - Complicated
- Complicated to set up a webserver
- Slower
- Able to use more mature technologies (popular frameworks, HTTPS)
- Less secure
    - Exposed to the internet, possibly hackable by anyone
- Playable from anywhere with an internet connection
    - Malicious people lock others out?
    - Or others can watch a random person play tetris?

**Method 2**
```
-----------------           -------------------
|      ESP      |           |      Phone      |
|  ('hotspot'   |  <----->  |  (connected to  |
|     mode)     |           |     hotspot)    |
-----------------           -------------------
```
- Harder to update source code, need to be close to ESP (see OTA)
- Redirect from URL (eg. tembusutetris.com to actual IP) with captive portal
    - URL only exists inside hotspot, may be confusing
- Requires disconnecting from internet, inconvenient
- More secure
    - No credentials for NUS_STU on ESP
    - ESP not exposed to Internet, not hackable by wider public
- Limited physical area to play game
    - Prevent random people playing :)
    - Prevent random people playing :(


### Redirecting URLs to the web server on the ESP
Method 2 has the drawback that a user needs to enter an IP address to reach a web server on the ESP, which isn't as user-friendly as a URL.
Using a method called DNS captive portal, we can resolve any URL (eg. google.com, or tembusutetris.com) to point to a server hosted on the ESP8266. Normally this resolving mechanism is carried out by dedicated servers around the world, but we can host on the ESP. This means we don't require an internet connection, and users have the convenience of entering a URL instead of an IP address.
In the following example,
1. A user connects to a hotspot created by the ESP8266 with name `DNSServer CaptivePortal example`.
2. They enter any url, eg. google.com
3. They are redirected to a web page that says `This is a captive portal example. All requests will be redirected here.`
Code taken from Arduino IDE -> File -> Examples -> DNSServer -> CaptivePortal.
```
#include <ESP8266WiFi.h>
#include <DNSServer.h>
#include <ESP8266WebServer.h>

const byte DNS_PORT = 53;
IPAddress apIP(192, 168, 1, 1);
DNSServer dnsServer;
ESP8266WebServer webServer(80);

String responseHTML = ""
                      "<!DOCTYPE html><html><head><title>CaptivePortal</title></head><body>"
                      "<h1>Hello World!</h1><p>This is a captive portal example. All requests will "
                      "be redirected here.</p></body></html>";

void setup() {
  WiFi.mode(WIFI_AP);
  WiFi.softAPConfig(apIP, apIP, IPAddress(255, 255, 255, 0));
  WiFi.softAP("DNSServer CaptivePortal example");

  // if DNSServer is started with "*" for domain name, it will reply with
  // provided IP to all DNS request
  dnsServer.start(DNS_PORT, "*", apIP);

  // replay to all requests with same HTML
  webServer.onNotFound([]() {
    webServer.send(200, "text/html", responseHTML);
  });
  webServer.begin();
}

void loop() {
  dnsServer.processNextRequest();
  webServer.handleClient();
}
```


### Updating the firmware over wifi
Uploading over Serial is fine during development, when you have access to the Serial pins and the USB port. But once your project is finished, and you put it inside an enclosure, it not that easy to upload updates with bug fixes or new features.
A solution to this problem is Over The Air updating, or OTA for short. As the name implies, this technology allows you to upload new code over Wi-Fi, instead of Serial.
The only disadvantage is that you have to explicitly add the code for it to every sketch you upload. You also need a flash chip that is twice the size of your sketch, so it won't work for 512kB boards. (It has to download the new sketch while still running the old code.)
```
/* Blinky program with Over-the-Air (OTA) update capability. */
#include <ESP8266WiFi.h>
#include <ArduinoOTA.h>

const byte led = 13;
const char* ssid     = "SSID";         // The SSID (name) of the Wi-Fi network you want to connect to
const char* password = "PASSWORD";     // The password of the Wi-Fi network

void setup() {
    Serial.begin(115200);         // Start the Serial communication to send messages to the computer

    WiFi.mode(WIFI_STA);  // Disable AP behaviour
    WiFi.begin(ssid, password);
    Serial.printf("Connecting to %s", ssid);

    while (WiFi.status() != WL_CONNECTED) {
        Serial.print(".");
        delay(500);
    }

    Serial.printf("\nConnected, IP address: %s\n", WiFi.localIP().toString().c_str());

    ArduinoOTA.setHostname("ESP8266");
    ArduinoOTA.setPassword("esp8266");

    ArduinoOTA.onStart([]() {
        Serial.println("Start");
    });
    ArduinoOTA.onEnd([]() {
        Serial.println("\nEnd");
    });
    ArduinoOTA.onProgress([](unsigned int progress, unsigned int total) {
        Serial.printf("Progress: %u%%\r", (progress / (total / 100)));
    });
    ArduinoOTA.onError([](ota_error_t error) {
        Serial.printf("Error[%u]: ", error);
        if (error == OTA_AUTH_ERROR) Serial.println("Auth Failed");
        else if (error == OTA_BEGIN_ERROR) Serial.println("Begin Failed");
        else if (error == OTA_CONNECT_ERROR) Serial.println("Connect Failed");
        else if (error == OTA_RECEIVE_ERROR) Serial.println("Receive Failed");
        else if (error == OTA_END_ERROR) Serial.println("End Failed");
    });

    ArduinoOTA.begin();
    Serial.println("OTA ready");

    pinMode(led, OUTPUT);
    digitalWrite(led, 1);
}

unsigned long previousTime = millis();

const unsigned long interval = 1000;

void loop() {
  ArduinoOTA.handle();
  unsigned long diff = millis() - previousTime;
  if(diff > interval) {
    digitalWrite(led, !digitalRead(led));  // Change the state of the LED
    previousTime += diff;
  }
}
```

Add your Wi-Fi credentials, and upload the sketch over Serial. Connect an LED (+ resistor) to pin 13. Then restart the IDE (you have to close all windows). Go to Tools > Port, and you should get a new option: Network Ports: ESP8266 at 192.168.1.x. Select it.

<img src="https://tttapa.github.io/Images/thumbs/ESP_OTA.png" width="70%" height="70%">


